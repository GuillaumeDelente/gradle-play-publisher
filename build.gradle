buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'org.gradle.api.plugins:gradle-nexus-plugin:0.3'
    }
}

apply plugin: 'groovy'
apply from: 'gradle-mvn-push.gradle'

group = GROUP
version = VERSION_NAME

sourceCompatibility = JavaVersion.VERSION_1_7
targetCompatibility = JavaVersion.VERSION_1_7

dependencies {

    repositories {
        jcenter()
    }

    compile gradleApi()

    compile 'com.android.tools.build:gradle:1.1.2'
    compile ('com.google.apis:google-api-services-androidpublisher:v2-rev13-1.19.0') {
        exclude group: 'com.google.guava', module: 'guava-jdk5'
    }
    compile 'commons-io:commons-io:2.4'

    testCompile 'org.mockito:mockito-all:1.9.5'
    testCompile 'org.hamcrest:hamcrest-library:1.3'
}

task fatJar(type: Jar) {
    baseName = project.name + '-all'
    from (configurations.compile.collect { logger.warn("${it}"); it.isDirectory() ? it : zipTree(it)}) {
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
    with jar
}

afterEvaluate { project ->
    uploadArchives {
        repositories {
            mavenDeployer {

                pom.project {
                    developers {
                        developer {
                            id 'bhurling'
                            name 'Bjoern Hurling'
                        }
                        developer {
                            id 'ChristianBecker'
                            name 'Christian Becker'
                        }
                        developer {
                            id 'GuillaumeDelente'
                            name 'Guillaume Delente'
                        }
                    }
                }

                pom.whenConfigured { pom ->
                    pom.dependencies = pom.dependencies.findAll { dep ->
                        dep.scope != 'test'
                    }
                }
            }
        }
    }
}